#!/bin/zsh

logdir=/var/log/rpmva
logfn=$logdir/rpmva.log
rcsfn=$logfn,v

function die { echo 1>&2 $*; exit 1 }

(($+commands[ci])) || die "rcs (ci) is not installed! exiting..."

[[ -d $logdir ]] || install -d -m 0700 $logdir || die "Can't mkdir $logdir"

nice rpm -Va > $logfn.tmp 2> $logfn.err

if [[ ! -s $logfn.err && -s $logfn.tmp ]]; then
  sort -k 1.13 $logfn.tmp > $logfn
else
  cat $logfn.err 1>&2
  die "Can't create rpmva log"
fi

[[ -e $rcsfn ]]; first_time=$?; # Note: inverted.

if ((first_time)) || ! rcsdiff -q $logfn; then
  ((first_time)) || rcs -l $rcsfn
  ci -q -u -wcron -mauto $logfn </dev/null
fi
