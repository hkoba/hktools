#!/bin/zsh

emulate -L zsh

config=/etc/sysconfig/syscheck.env

progname=$(readlink -f $0)

function cmd_install {
    [[ -r $config ]] || cp -v $progname.sample $config
    ln -sv $progname /etc/cron.daily/yz0-syscheck.cron
    exit
}

zparseopts -D -K x=o_xtrace

if (($#o_xtrace)); then set -x; fi

if ((ARGC)); then
    cmd=$1; shift
    cmd_$cmd "$@"
fi

[[ -r $config ]] || exit

set -e
source $config

# [0] Install packages

yum_or_dnf=""
for p in dnf yum; do
    if (($+commands[$p])); then
        yum_or_dnf=$p
        break;
    fi
done

if [[ -n $yum_or_dnf ]]; then
    missing=()
    for pkg in $required_packages; do
       rpm -q $pkg >& /dev/null && continue
       missing+=($pkg)
    done
    if (($#missing)); then
        $yum_or_dnf install -y $missing || true
    fi
fi

# [1] GIT difference notification

if ((! $+gitwatches && $+svkwatches)); then
    gitwatches=($svkwatches)
fi

for d in $gitwatches; do
   [[ -d $d ]] || continue
   pushd $d
   {
       git status -s
   } always {
       popd
   }
done

# [2] permission enforcement

zmodload zsh/stat
for d should in $perm_ensure; do
   now=${"$(stat -o +mode $d)"[-4,-1]}
   [[ $now = $should ]] && continue
   print "$d should be changed to $should (from $now)"
   chmod $should $d
done

# [3] ownership enforcement
for d should in $owner_ensure; do
    stat -sH now $d || continue
    ug=(${(s/:/)should})
    if (($#ug == 2)); then
	[[ $now[uid] == $ug[1] && $now[gid] == $ug[2] ]] && continue
    else
	[[ $now[uid] == $ug[1] ]] && continue
    fi
    print "$d should be changed to $should (from $now[uid]:$now[gid])"
    chown $should $d
done

# [4] should be removed even after yum.

for f in $ensure_removed; do
    [[ -e $f ]] || continue
    print "$f should be removed"
    rm -f $f
done

# [5] rpm should be removed

for pkg in $rpm_ensure_removed; do
   rpm -q $pkg 2>/dev/null || continue
   print "$pkg should be removed"
   rpm -e $pkg
done

# [6] systemd services which should be enabled and running

for svc in $systemd_services; do
    if ! systemctl --quiet is-enabled $svc; then
	print "$svc should be enabled"
	systemctl enable $svc || true
    fi
    if ! systemctl --quiet is-active $svc; then
	print "$svc should be active"
	systemctl start $svc || true
    fi
done
