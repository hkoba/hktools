#autoload -*- mode: shell-script; sh-shell: zsh; coding: utf-8 -*-

emulate -L zsh

setopt localoptions extendedglob errreturn

local usage="Usage: $0 HOST"

if ((! ARGC)); then
    print $usage
    return 1
fi

local host=$1; shift

local tmpDir
if [[ -d /run/user && -d /run/user/$UID ]]; then
    tmpDir=/run/user/$UID
elif [[ -d ~/.ssh ]]; then
    tmpDir=~/.ssh/tmp
    mkdir -p $tmpDir
else
    echo "Can't find private tempdir, stopped"
    return 1
fi
local scriptFn=$tmpDir/git-ssh-$host
local ctrlDir=$scriptFn.d
local ssh_ctrl=$ctrlDir/ssh-%h%p

sed -e "s,@ORIG_HOST@,$host,g" -e "s,@SSH_CTRL@,$ssh_ctrl,g" > $scriptFn <<'EOF'
#!/bin/zsh
zparseopts -D -K p=o_port
host=$1; shift
orig_host=@ORIG_HOST@
if [[ $host == $orig_host ]]; then
  ssh -S @SSH_CTRL@ $o_port $host "$@"
else
  ssh -A -S @SSH_CTRL@ $orig_host ssh -q $o_port $host "$@"
fi
EOF

chmod a+x $scriptFn

mkdir $ctrlDir

zmodload zsh/zpty
zpty GIT_SSH-$host ssh -A -M -o ControlPath=$ssh_ctrl $host /bin/sh
# -o ControlPersist=yes

export GIT_SSH=$scriptFn

echo GIT_SSH=$scriptFn
