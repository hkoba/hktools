#autoload -*- mode: shell-script; sh-shell: zsh; coding: utf-8 -*-

emulate -L zsh

setopt local_options extended_glob err_return

local o_quiet o_help
local usage="Usage: $0 [-h] [-q] HOST"

zparseopts -D -K q=o_quiet h=o_help

if ((! ARGC)) || (($#o_help)); then
    print $usage
    return 1
fi

local host=$1; shift

local tmpDir
if [[ -d /run/user && -d /run/user/$UID ]]; then
    tmpDir=/run/user/$UID
elif [[ -d ~/.ssh ]]; then
    tmpDir=~/.ssh/tmp
    mkdir -p $tmpDir
else
    echo "Can't find private tempdir, stopped"
    return 1
fi
local scriptFn=$tmpDir/git-ssh-$host
local ctrlDir=$scriptFn.d
local ssh_ctrl=$ctrlDir/ssh-%h%p

if [[ ! -e $scriptFn ]]; then
    (($#o_quiet)) || echo Creating proxy script $scriptFn
sed -e "s,@ORIG_HOST@,$host,g" -e "s,@SSH_CTRL@,$ssh_ctrl,g" > $scriptFn <<'EOF'
#!/bin/zsh
zparseopts -D -K p=o_port
host=$1; shift
orig_host=@ORIG_HOST@
if [[ $host == $orig_host ]]; then
  ssh -S @SSH_CTRL@ $o_port $host "$@"
else
  ssh -A -S @SSH_CTRL@ $orig_host ssh -q $o_port $host "$@"
fi
EOF
fi

[[ -x $scriptFn ]] || chmod a+x $scriptFn

[[ -d $ctrlDir ]] || mkdir -p $ctrlDir

zmodload zsh/zpty
zpty -b GIT_SSH-$host ssh -A -M -o ControlPath=$ssh_ctrl $host /bin/sh
# -o ControlPersist=yes

export GIT_SSH=$scriptFn

(($#o_quiet)) || echo GIT_SSH=$scriptFn
