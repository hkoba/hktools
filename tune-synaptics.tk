#!/usr/bin/env wish
# -*- coding: utf-8 -*-

package require snit
package require widget::scrolledwindow

snit::widget pointerevents {
    option -synclient synclient

    component myListener
    component myPane
    component myCanv -public canvas
    component myList
    variable mySynConfigs {}

    constructor args {
	$self build toplevel
	$self configurelist $args
	$self Reload
    }
    method {build toplevel} {} {
	$self build menu

	set vbox [panedwindow $win.vbox -orient vertical]
	$vbox add [set sw [widget::scrolledwindow $myPane.sw[incr i]]]
	install myListener using text $sw.listener -height 3
	$sw setwidget $myListener

	install myPane using panedwindow $vbox.hbox -orient horizontal
	$vbox add $myPane

	$myPane add [set sw [widget::scrolledwindow $myPane.sw[incr i]]]
	install myList using listbox [set w $sw.lb] \
	    -listvar [myvar mySynConfigs]
	$sw setwidget $w
	
	$myPane add [set sw [widget::scrolledwindow $myPane.sw[incr i]]]
	install myCanv using canvas [set w $myPane.sw[incr i]]
	$sw setwidget $w

	$self bind canvas

	# pack $myPane -fill both -expand yes

	pack $vbox -fill both -expand yes
    }
    method {build menu} {} {
	[winfo toplevel $win] configure -menu [menu [set m $win.menu]]
	wm protocol [winfo toplevel $win] WM_DELETE_WINDOW [list $self Quit]

	$m add cascade -label File -menu [menu $m.file]
	$m.file add command -label Console -command [list $self console]
    }

    method {bind canvas} {} {
	bind $myCanv <ButtonPress-1> [list $self Single Press 1]
	bind $myCanv <ButtonRelease-1> [list $self Single Release 1]
	bind $myCanv <B1-Motion> [list $self Single Motion 1]
	bind $myCanv <Double-ButtonPress-1> [list $self Double Press 1]
	bind $myCanv <Double-ButtonRelease-1> [list $self Double Release 1]
	bind $myCanv <Double-B1-Motion> [list $self Double Motion 1]
    }

    method Single args { $self log Single $args }
    method Double args { $self log Double $args }
    method log args {
	$myListener see end
	$myListener insert end $args\n
    }


    method Reload {} {
	set mySynConfigs [$self param list]
    }

    method {param list} {} {
	set list {}
	foreach line [split [exec $options(-synclient)] \n] {
	    if {![regexp {^\s+(\w+)\s+=\s+(\d+)} $line -> key val]} continue
	    lappend list [list $key $val]
	}
	set list
    }

    method Quit {} {
	exit
    }

    method console {} {
	package require tclreadline
	tclreadline::readline eofchar [list $self Quit]
	after idle tclreadline::Loop
    }
}

if {![info level] && [info script] eq $::argv0} {
    wm geometry . 1000x900
    pack [pointerevents .win] -fill both -expand yes
    catch {after 500 {after idle {.win console}}}
}
